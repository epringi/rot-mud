#!/bin/csh
# Written by Furey.
# With additions from Tony and Alander.
# Tweaks by Liz Pringi to bring the script into 2020

# Set the port number and grab it from the command line if supplied.
set port = 9400
if ( "$1" != "" ) set port="$1"

# Set limits.
if ( -e shutdown.txt ) rm -f shutdown.txt

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
	set logfile = ../log/$index.log
	if ( ! -e $logfile ) break
	@ index++
    end

    # Run rom.
    # Check if already running
    echo "Checking if MUD is already running on port $port"
    set matches = `lsof -i:$port -t`
    if ( $matches >= 1 ) then
        # Already running
        echo "MUD Already running."
        exit 0
    endif
    echo "Starting MUD..."
    ../bin/rom $port >&! $logfile


    # Restart, giving old connections a chance to die.
    if ( -e shutdown.txt ) then
	echo "MUD terminated."
	rm -f shutdown.txt
	exit 0
    endif
    echo "MUD restarting..."
    sleep 3
end
